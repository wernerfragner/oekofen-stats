plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.graalvm.buildtools.native' version '0.9.19'
}

group = 'org.oekofen'
version = '0.3.2'
sourceCompatibility = '17'

repositories {
    maven { url 'https://repo.spring.io/release' }
    mavenCentral()
}

dependencies {
    implementation 'com.influxdb:influxdb-client-java:6.7.0'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        // web server currently not needed
        exclude module: 'spring-boot-starter-tomcat'
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}


tasks.named('bootBuildImage') {
    builder = 'paketobuildpacks/builder:tiny'
    environment = ['BP_NATIVE_IMAGE': 'false']
}


tasks.register('copyLibs', Copy) {
    group 'build setup'
    from layout.buildDirectory.dir("libs")
    into layout.buildDirectory.dir("setup")
}

tasks.register('copySetup', Copy) {
    group 'build setup'
    from layout.buildDirectory.dir("../setup")
    into layout.buildDirectory.dir("setup")
}

tasks.register('copyConfigs', Copy) {
    group 'build setup'
    from layout.buildDirectory.dir("../config")
    into layout.buildDirectory.dir("setup/config")
}

task buildSetupFolder(dependsOn: ["bootJar", "copyLibs", "copySetup", "copyConfigs"])
{
    group 'build setup'
}

tasks.register('copyLibsDocker', Copy) {
    group 'build setup'
    from layout.buildDirectory.dir("libs")
    into layout.buildDirectory.dir("docker")
}

tasks.register('copyConfigsDocker', Copy) {
    group 'build setup'
    from layout.buildDirectory.dir("../docker/arm32v7/")
    into layout.buildDirectory.dir("docker")
}

task buildDockerFolder(dependsOn: ["bootJar", "copyLibsDocker", "copyConfigsDocker"])
{
    group 'build setup'
}




